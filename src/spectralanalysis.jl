module spectralanalysis

using ..states, ..operators, ..operators_sparse

export operatorspectrum, operatorspectrum_hermitian, eigenbasis, eigenbasis_hermitian, groundstate

function operatorspectrum_hermitian(H::Operator; Nmax::Union{Int, Void}=nothing)
    h = Hermitian(H.data)
    return Nmax == nothing ? eigvals(h) : eigvals(h, 1:Nmax)
end

operatorspectrum_hermitian(H::SparseOperator; Nmax::Union{Int, Void}=nothing) = real(operatorspectrum(H; Nmax=Nmax))

function operatorspectrum(H::Operator; Nmax::Union{Int, Void}=nothing)
    if ishermitian(H.data)
        return operatorspectrum_hermitian(H; Nmax=Nmax)
    end
    s = eigvals(H.data)
    return Nmax == nothing ? s : s[1:Nmax]
end

function operatorspectrum(H::SparseOperator; Nmax::Union{Int, Void}=nothing)
    if Nmax == nothing
        Nmax = size(H.data, 2) - 2
    end
    d, nconv, niter, nmult, resid = eigs(H.data; nev=Nmax, which=:SR, ritzvec=false)
    return d
end

function eigenbasis_hermitian(H::Operator; Nmax::Union{Int, Void}=nothing)
    h = Hermitian(H.data)
    M = Nmax == nothing ? eigvecs(h) : eigvecs(h, 1:Nmax)
    b = Ket[]
    for k=1:size(M,2)
        push!(b, Ket(H.basis_r, M[:,k]))
    end
    return b
end

function eigenbasis(H::Operator; Nmax::Union{Int, Void}=nothing)
    if ishermitian(H.data)
        return eigenbasis_hermitian(H; Nmax=Nmax)
    end
    M = eigvecs(H.data)
    b = Ket[]
    for k=1:size(M,2)
        if Nmax!=nothing && k>Nmax
            break
        end
        push!(b, Ket(H.basis_r, M[:,k]))
    end
    return b
end

eigenbasis_hermitian(H::SparseOperator; Nmax::Union{Int, Void}=nothing) = eigenbasis(H; Nmax=Nmax)

function eigenbasis(H::SparseOperator; Nmax::Union{Int, Void}=nothing)
    if Nmax == nothing
        Nmax = size(H.data, 2) - 2
    end
    d, M, nconv, niter, nmult, resid = eigs(H.data; nev=Nmax, which=:SR, ritzvec=true)
    b = Ket[]
    for k=1:size(M,2)
        push!(b, Ket(H.basis_r, M[:,k]))
    end
    return b
end

groundstate(H::Union{Operator, SparseOperator}) = eigenbasis_hermitian(H; Nmax=1)

end # module
